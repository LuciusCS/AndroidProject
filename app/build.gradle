apply plugin: 'com.android.application'

//用于引入配置信息 config.gradle，别忘了在工程gradle中添加 apply from:"config.gradle"
def androidId=rootProject.ext.androidId
def mode1Id=rootProject.ext.mode1Id
def mode2Id=rootProject.ext.mode2Id
def upgradeUrl=rootProject.ext.upgradeUrl

//用于获取当前的时间
static def releaseTime(){
    return new Date().format("yyyy-MM-dd-HH.mm",TimeZone.getTimeZone("GMT-8"))
}


android {
    compileSdkVersion 28
    defaultConfig {
//        applicationId "demo.lucius.androidproject"
        minSdkVersion 24
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            //不显示打包log
            buildConfigField( "boolean","LOG_DEBUG","false")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug{
            //显示打包log
            buildConfigField( "boolean","LOG_DEBUG","true")
            applicationIdSuffix ".debug"
            debuggable true

        }
    }

    //使用databinding
    dataBinding {
        enabled = true
    }

    flavorDimensions "mode1"

    productFlavors{
        mode1{
            applicationId mode1Id.applicationId
            versionCode mode1Id.versionCode
            versionName mode1Id.versionName
            dimension "mode1"
            applicationIdSuffix ".mode1"
            versionNameSuffix "_mode1"
            //用于配置升级的url
            buildConfigField "String","upgradeUrl","\"${upgradeUrl.mode1Upgrade}\""

            //不同渠道在资源文件中添加不同的字段，可直接通过@string/modeTest调用
            resValue("string","modeTest","mode1")



            //用于配置mainfest中的信息
            manifestPlaceholders=[
                    aMapId: "13414332",
                    //配置不同的应用名称和图片
                    app_icon: "@mipmap/ic_launcher",
                    app_name: "mode1"
            ]
        }
        mode2{
            applicationId mode2Id.applicationId
            versionCode mode2Id.versionCode
            versionName mode2Id.versionName
            dimension "mode1"
            versionNameSuffix "_mode2"
            applicationIdSuffix ".mode2"
            buildConfigField( "String","upgradeUrl","\"${upgradeUrl.mode2Upgrade}\"")

            //不同渠道在资源文件中添加不同的字段
            resValue("string","modeTest","mode2")

            //用于配置mainfest中的信息
            manifestPlaceholders=[
                    aMapId: "1341433ewewew2",
                    //配置不同的应用名称和图片
                    app_icon: "@mipmap/ic_launcher",
                    app_name: "mode2"
            ]
        }
    }

    //打包时设置名称和时间，区分不同渠道包
    applicationVariants.all{
        variant->
            variant.outputs.all{
                outputFileName="${variant.productFlavors[0].name}-v${variant.productFlavors[0].versionName}-${releaseTime()}.apk"
            }
    }

}

dependencies {

    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraint_layout_version"
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"

    //引入本地jar
    //引入libs下的所有的jar
    implementation fileTree(dir:'libs',includes: ['*.jar'])
    //指定一个或某几个jar
   // implementation files('libs/dict-v120.jar', 'libs/download-v151.jar')

    //引入libs下的所有aar
    implementation fileTree(dir:'libs',includes: ['*.aar'])
    //依赖某一个aar
  //  implementation(name:'library-download',ext:'aar')


    //引入本地moudle library
    //用于使用AspectJlib
    implementation project(':AspectJLib')
    implementation "org.aspectj:aspectjrt:$rootProject.aspectj_version"

    //添加DataBinding


    // (Java only)
    implementation "androidx.work:work-runtime:$rootProject.work_version"
    // optional - RxJava2 support
    implementation "androidx.work:work-rxjava2:$rootProject.work_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycle_version"

    //用于添加Dagger2
    implementation 'com.google.dagger:dagger-android:2.17'
    implementation 'com.google.dagger:dagger-android-support:2.17' // if you use the support libraries
    implementation 'com.google.dagger:dagger:2.17'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.17'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    //不同的渠道包依赖不同的版本
    mode1Implementation  'com.google.dagger:dagger-android:2.17'
    mode2Implementation   'com.google.dagger:dagger-android:2.17'



}


//用于AspectJ织入代码
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main
final def log = project.logger

android.applicationVariants.all{ variant ->
    if (!variant.buildType.isDebuggable()) {
        log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
        return
    }

    JavaCompile javaCompile = variant.javaCompiler
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.8",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
        log.debug "ajc args: " + Arrays.toString(args)

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break
                case IMessage.WARNING:
                    log.warn message.message, message.thrown
                    break
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break
            }
        }
    }
}